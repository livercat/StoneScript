/*
Copyright © 2019-2022, Matjaž Guštin
<dev@matjaz.it> <https://matjaz.it>
Released under the BSD 3-clause license.

Level: Icy Ridge
*/

var hrminir_encountered = false
  
?loc = icy_ridge
  // Small enemies
  ?foe = elemental | foe = ki
    ?hp = maxhp | foe.distance <= 23
      // Healed completely or enemy too close
      equip_staff("fire")
    :
      // Try to heal while attacking from afar
      equip_ranged_healing("fire")
  :?foe = pillar | foe = ice_wall
    ?foe.distance <= 5
      equip blade_of_god
      ?hrminir_encountered &
      ^ item.GetCooldown("blade_of_god") <= 0
        // Hit the wall and the boss
        // behind it as well. Don't use `R`
        // before the boss to avoid waiting
        // for the cooldown.
        activate R
    :
      equip_dashing("fire")

  // Hrímnir
  :?foe = boss
    hrminir_encountered = true
    ?foe.distance <= 2
      ?foe.armor > 0
        equip_armor_piercing("fire")
      :
        equip_dps_melee("fire")
    :?foe.distance <= 5
      // The shovel has an attack range of 1
      // so it forces the player to move very
      // very close. So close that it can avoid
      // the attacks (snowballs)
      equip shovel
    :?foe.distance > 5
      equip_dashing("fire")

  healwalk()

