/*
Copyright © 2019-2022, Matjaž Guštin
<dev@matjaz.it> <https://matjaz.it>
Released under the BSD 3-clause license.

Level: Boiling Mine
*/

?loc = mine
  // Small enemies
  ?foe = master
  ^ | foe = elemental
  ^ | foe = geyser
  ^ | foe = walker
    ?foe.distance > 8
      equip_dashing("aether")
    :?foe.armor > 0
      equip_armor_piercing("aether")
    :
      equip_1h_melee("aether")
  :?hp < 10
    activate potion
  healwalk()

?loc = bronze_guardian
  ?hp <= 6
    activate potion
  ?foe ! boss
    equip rep
  ?foe = boss
    ?foe.state=32 & foe.time=22
      equipR mind stone
    ?foe.state=33
      ?foe.distance>7
        equipL sword
        equipR dashing
      ?foe.distance<=6 & hp>=30 & foe.armor>0
        equip he
    ?foe.state ! 33 & foe.state ! 32 &
      ^item.getcooldown("hammer")<=646
      equip rep
    ?foe = boss & foe.distance <= 20 &
    ^item.GetCooldown("hammer") <=0
      equip hea
      ?item.CanActivate("hammer")
        activate R
  healwalk()