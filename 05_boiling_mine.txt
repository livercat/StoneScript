/*
Copyright © 2019-2022, Matjaž Guštin
<dev@matjaz.it> <https://matjaz.it>
Released under the BSD 3-clause license.

Level: Boiling Mine
*/

?loc = mine
  // Small enemies
  ?foe = master
  ^ | foe = elemental
  ^ | foe = geyser
  ^ | foe = walker
    ?foe.distance > 8
      equip_dashing("aether")
    :?foe.armor > 0
      equip_armor_piercing("aether")
    :
      equip_1h_melee("aether")
  :?hp < 10
    activate potion
  healwalk()

?loc = bronze_guardian
  ?time > 600 & foe.distance > 10
    // Guardian is now attackable from afar
    equip_ranged_shield("aether")
  ?pos.y > 0 & foe.distance > 5
    // Guardian hits, we jump on its hand
    equip_dashing("aether")
  :?foe.distance <= 5
    // We are on its arm, close to its face
    ?foe.armor > 0
      equip_armor_piercing("aether")
    :
      equip_dps_melee("aether")
  :?hp < 10
    activate potion
  healwalk()