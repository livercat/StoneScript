
func equip_1_weapon(weapon)
  equip @weapon@

func equip_2_weapons(weaponL, weaponR)
  equipL @weaponL@
  equipR @weaponR@

// Place this function at the default case
// of any switch-case
func healwalk()
  // Pick up materials quickly with Star stone
  ?pickup.distance < 10
    equip_2_weapons("star", "compound shield *4 + 1")
  // Heal and gain armor while just walking
  // towards next objective. If 100% healed,
  // walk faster.
  :?target = waypoint | foe.distance > 22
    ?hp < maxhp
      equip_2_weapons("ouroboros", "compound shield *4 + 1")
    :
      equip_2_weapons("triskelion", "compound shield *4 + 1")



?loc = rocky
  equip shovel
  healwalk()

  // Boss: Dysangelos, phase 1 (base form)
  ?foe = dysangelos_bearer
    equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")

  // Boss: Dysangelos, phase 2 (elemental form)
  ?foe = dysangelos_elementalist
    equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
    // TODO use the proper runes against
    // the current rune in the eye of the boss

  // Boss: Dysangels, phase 3 (perfect form)
  ?foe = dysangelos_perfected
    equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
    ?foe.armor > 0
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    // Their most powerful move deals 30 damage
    // points on 5* difficulty, so we need to
    // heal if we don't have enough health and
    // armor combined
    ?(hp + armor) < 31
      activate potion

?loc = deadwood
  // Small enemies
  ?foe = mosquito
    ?foe.distance >= 5
      equip_2_weapons("crossbow *1 + 3 fire", "wand *3 vigor")
    :
      equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
  :?foe = scarab
    equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
  :?foe = boss | foe = poena
    // Boss: Xyaloalgia, residue of Leuce
    // and Poena, its phase 2
    ?foe.distance >= 8
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
  ?hp <= 10
    activate potion

  healwalk()
  
  ?harvest.distance < 10
    // Cut down trees with the hatchet
    // using the fast-cutting when available
    equip hatchet
    ?harvest.distance <= 4
    ^& item.GetCooldown("hatchet") <= 0
      activate R

?loc = caves
  ?foe ! boss  // Small enemies
    ?hp = maxhp | foe.distance <= 23
      // Healed completely or enemy too close
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      // Try to heal while attacking from afar
      equip_2_weapons("crossbow *1 + 3 fire", "ouroboros")
  :?foe = boss  // Bolesh, the cunning
    ?foe.distance > 10
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
  :?hp < 10
    activate potion

  healwalk()

?loc = fungus_forest
  ?hp = maxhp | foe.distance <= 23
    // Healed completely or enemy too close
    equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  :
    // Try to heal while attacking from afar
    equip_2_weapons("crossbow *1 + 3 fire", "ouroboros")
  ?hp <= 10
    activate potion
  healwalk()

?loc = fungus_forest_boss  // Boss room
  ?foe = mushroom_boss_skinny  // Enoki
    equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  :?foe = mushroom_boss // Angry or Morel
    ?foe.distance >= 6
      // Morel's hand keeps you at distance 5
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :?foe.armor > 0
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
  ?hp <= 10
    activate potion
  healwalk()

?loc = undead_crypt_boss | loc = undead_crypt
  ?foe ! boss  // Small enemies
    ?hp = maxhp | foe.distance <= 23
      // Healed completely or enemy too close
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  :?foe = boss
    equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  :?hp < 10
    activate potion

  healwalk()

?loc = mine
  // Small enemies
  ?foe = master
  ^ | foe = elemental
  ^ | foe = geyser
  ^ | foe = walker
    ?foe.distance > 8
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :?foe.armor > 0
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      equip_2_weapons("big stone sword *4 + 1", "compound shield *4 + 1")
  :?hp < 10
    activate potion
  healwalk()

?loc = bronze_guardian
  ?time > 600 & foe.distance > 10
    // Guardian is now attackable from afar
    equip_2_weapons("crossbow *1 + 3 fire", "compound shield *4 + 1")
  ?pos.y > 0 & foe.distance > 5
    // Guardian hits, we jump on its hand
    equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  :?foe.distance <= 5
    // We are on its arm, close to its face
    ?foe.armor > 0
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  :?hp < 10
    activate potion
  healwalk()

var hrminir_encountered = false
  
?loc = icy_ridge
  // Small enemies
  ?foe = elemental | foe = ki
    ?hp = maxhp | foe.distance <= 23
      // Healed completely or enemy too close
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :
      // Try to heal while attacking from afar
      equip_2_weapons("crossbow *1 + 3 fire", "ouroboros")
  :?foe = pillar | foe = ice_wall
    ?foe.distance <= 5
      equip blade_of_god
      ?hrminir_encountered &
      ^ item.GetCooldown("blade_of_god") <= 0
        // Hit the wall and the boss
        // behind it as well. Don't use `R`
        // before the boss to avoid waiting
        // for the cooldown.
        activate R
    :
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")

  // HrÃ­mnir
  :?foe = boss
    hrminir_encountered = true
    ?foe.distance <= 2
      ?foe.armor > 0
        equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
      :
        equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
    :?foe.distance <= 5
      // The shovel has an attack range of 1
      // so it forces the player to move very
      // very close. So close that it can avoid
      // the attacks (snowballs)
      equip shovel
    :?foe.distance > 5
      equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")

  healwalk()



?loc = temple
  equip_2_weapons("big stone sword *4 + 1", "crossbow *1 + 3 fire")
  ?hp <= 10
    activate potion

?loc = nagaraja
  equip blade_of_god
  ?hp <= 10 | debuffs.count >= 8
    activate potion