var armor_shield = "stone shield *6"
var ah_shield = "vigor shield ah"
var dashing = "dashing shield"

var fire_debuff = "fire sword dF"
var poison_debuff = "posion sword dP"
var ice_debuff = "ice hammer dI"
var aether_dps = "aether hammer D"
var vigor_dps = "TODO"

var melee_dps = "fire sword dF"
var melee_offhand = "aether hammer D"

var ranged_1h = "poison wand D"
var ranged_dps = "repeating crossbow"

func equip_shield()
  ?hp < maxhp
    equipR @ah_shield@
  :
    equipR @armor_shield@

func equip_offhand(mode)
  ?mode = "dps"
    equipR @melee_offhand@
  :?mode = "def"
    equip_shield()

func healwalk()
  ?hp < 10
    activate potion
  ? ! foe | foe.distance > 22
    equip_shield()
    ?pickup.distance < 8
      equipL star
    :?target = waypoint | foe.distance > 22
      ?hp < maxhp
        equipL ouroboros
      :
        equipL triskelion

func pickpocket()
  equip skeleton arm
  ?foe.hp <= 32 &
  ^item.CanActivate("skeleton_arm")
    activate R

func melee(mode)
  ?foe.distance < 23

    ?foe.distance < 7 & item.GetCooldown("skeleton_arm") <= 0 |
    ^buffs.string ! "pick_pocket:1"
      pickpocket()
    :
      ?foe.distance >= 7
        equipR @dashing@
      :
        equip_offhand(mode)

      ?foe = aether
        equipL @poison_debuff@ // vigor
      :?foe = fire
        equipL @aether_dps@
      :?foe = ice
        equipL @fire_debuff@
      :?foe = poison
        equipL @ice_debuff@
      :?foe = vigor
        equipL @poison_debuff@
      :
        equipL @melee_dps@

func melee_boss(mode)
  ?foe.distance < 23
    // hammer debuff
    ?foe.armor > 0 & foe.distance <= 10 &
    ^item.GetCooldown("hammer") <= 0 |
    ^item.GetCooldown("hammer") >= 647 // ios needs more time
      equip heavy hammer
      ?item.CanActivate("hammer")
        activate R

    :?foe.buffs.string ="buff_protection"
      melee(mode)
    :?foe.debuffs.string = "∞:debuff_damage:1:"| foe = immune_to_debuff_damage
      ?foe.debuffs.string = "φ:debuff_dot:1:"| foe = immune_to_debuff_dot
        ?foe.debuffs.string = "❄:debuff_chill:1:"| foe = immune_to_debuff_chill
          melee(mode)
        :
          equip_offhand(mode)
          equipL @ice_debuff@
      :
        equip_offhand(mode)
        equipL @fire_debuff@
    :
      equip_offhand(mode)
      equipL @poison_debuff@

func ranged(mode)
  ?foe.distance < 23
    ?foe.distance < 7 & item.GetCooldown("skeleton_arm") <= 0 |
    ^buffs.string ! "pick_pocket:1"
      pickpocket()
    :?foe.distance >= 8
      ?mode = "dps"
        equip @ranged_dps@
      :?mode = "def"
        equipL @ranged_1h@
        equip_offhand(mode)
    :
      melee(mode)

?loc = rocky
  var dashing = "vigor shield ah" // disable dashing to avoid the ray
  healwalk()
  melee_boss("def")
  ?foe.state = 115 & foe.time = 60 // dodge the ray
    equipL mind stone

?loc = deadwood
  healwalk()
  ?foe = mosquito
    ranged("dps")
  :?foe = scarab
    melee("def")
  :
    melee_boss("def")

?loc = caves
  healwalk()
  ?foe ! bosss
    ranged("dps")
  :
    melee_boss("def")

?loc = fungus_forest | loc = fungus_forest_boss
  healwalk()
  ?foe ! boss
    ranged("dps")
  :
    melee_boss("def")

?loc = undead_crypt | loc = undead_crypt_boss
  healwalk()
  ?foe = ghost | foe = large_ghost | foe = ghost_tomb // immune to physical
    equipL @ranged_1h@
    equip_offhand(mode)
  :?foe ! boss
    ranged("dps")
  :
    melee_boss("def")

?loc = mine | loc = bronze_guardian
  healwalk()
  ?foe ! boss
    melee("def")  // they're all ranged
  :?foe.state = 32 & foe.time = 22 // dodge hammer
    equipR mind stone
  :?foe.state = 33 // hammer is down
    ?foe.distance > 7
      equipL triskelion
      equipR @dashing@
    :?foe.distance <= 7
      ?foe.armor > 0 &
      ^item.GetCooldown("hammer") <= 0 |
      ^item.GetCooldown("hammer") >= 647 // ios needs more time
        equip heavy hammer
        ?item.CanActivate("hammer")
          activate R
      :
        equipL @melee_dps@
        equipR @melee_offhand@
  :
    equip @ranged_dps@

?loc = icy_ridge
  var hrminir_encountered = false
  healwalk()
  ?foe = elemental | foe = ki
    melee("def")
  :?foe = pillar | foe = ice_wall
    ?hrminir_encountered
      melee("def")
    :
      ranged("def")
  :?foe = boss
    hrminir_encountered = true
    ?foe.distance <= 2
      melee_boss("dps")
    :?foe.distance <= 5
      // The shovel has an attack range of 1
      // so it forces the player to move very
      // very close. So close that it can avoid
      // the attacks (snowballs)
      equip shovel
    :
      equipL @melee_dps@
      equipR @dashing@

?loc = temple | loc = nagaraja
  healwalk()
  ?foe ! boss
    melee("def")
  :
    melee_boss("dps")