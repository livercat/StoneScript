var armor_shield = "stone shield *8"
var regen_shield = armor_shield
var dashing = "dashing shield *7"
var bashing = "bashing shield"

var fire_1h = "fire sword *7 dF"
var poison_1h = "posion sword *7 dP"
var ice_1h = "ice sword *5 dI"
var aether_1h = "aether sword *7 D"
var vigor_1h = "vigor sword *7 dL"

var elemental = "vigor staff *7"
var ranged_1h = ""
var ranged_2h = "repeating crossbow *8"

var bardiche = "bardiche *7"
var blade = "blade of the fallen god"
var arm = "skeleton arm"

var arm_damage = 32
var pickpocket = "pick_pocket:2"
var chill = "❄:debuff_chill:3:"

var melee_dps = vigor_1h
var melee_offhand = fire_1h
var melee_aoe = blade

// internal use
var _main_hand = ""
var _naga = 0
var x = 0
var y = 0
var cache = []

?loc.loop
  _main_hand = ""
  _naga = 0
  x = 0
  y = 0
  cache.Clear()

func healwalk()
  ?foe & hp < 10
    activate potion
  ?foe & foe.distance < 23
    return
  ?pickup.distance < 8
    equipL star
    equip_shield()
  :?target = waypoint | foe.distance > 22
    var cd = item.GetCooldown("quarterstaff")
    ?(cd <= 0 | cd > 185) & item.CanActivate() &
    ^ai.enabled
      equip quarterstaff
      ?item.CanActivate("quarterstaff")
        activate R
    :?hp < maxhp
      equipL ouroboros
      equip_shield()
    :
      equipL triskelion
      equip_shield()

func dash()
  ?foe.distance > 6 & 
  ^ai.enabled &
  // don't dash into ray
  ^(foe ! dysangelos_perfected | foe.state ! 115)
    ?item.GetCooldown("bash") <= 0
      return bashing
    :?item.GetCooldown("dash") <= 0
      return dashing
  return false
  

func equip_offhand(mode)
  var dasher = dash()
  ?dasher ! false
    equipR @dasher@
  :?mode = "dps"
    ?_main_hand = melee_dps
      equipR @melee_offhand@
    :
      equipR @melee_dps@
  :?mode = "def"
    equip_shield()

func equip_shield()
  ?hp < maxhp
    equipR @regen_shield@
  :
    equipR @armor_shield@

func should_slap()
  ?foe = pallas // immune
    return false
  ?!ai.enabled // skills disabled
    return false
  ?foe.distance > 6
    return false
  ?buffs.string ! pickpocket
    return true
  ?item.GetCooldown("skeleton_arm") > 0
    return false
  return foe.armor <= 0 & foe.hp <= arm_damage

func slap()
  equip @arm@
  ?item.CanActivate("skeleton_arm") &
  ^buffs.string = pickpocket
    activate R

func should_hammer()
  ?!ai.enabled // skills disabled
    return false
  ?foe.armor <= 0
    return false
  ?foe.distance > 7
    return false
  // delay hammering until shield animation ends
  ?foe = dysangelos_perfected &
  ^foe.state < 107
    return false
  var cd = item.GetCooldown("hammer")
  return cd <= 0 | cd >= 647

func hammer()
  equip heavy hammer
  ?item.CanActivate("hammer")
    activate R

func should_smite()
  ?foe = pallas // immune
    return false
  ?!ai.enabled // skills disabled
    return false
  ?foe.distance > 11
    return false
  ?item.GetCooldown("blade") > 0
    return false
  return (foe.count >= 5 | foe = boss)

func smite()
  equip @blade@
  ?item.CanActivate("blade")
    activate R

func should_bardiche()
  ?!ai.enabled
    return false
  ?foe.distance > 9
    return false
  ?foe ! boss
    return false
  var cd = item.GetCooldown("bardiche")
  return cd <= 0 | cd >= 870

func bardiche()
  equip @bardiche@
  ?item.CanActivate("bardiche")
    activate R

func equip_main_hand(weapon)
  _main_hand = weapon
  equipL @weapon@

func melee(mode)
  _main_hand = ""
  ?mode = "aoe"
    equip @melee_aoe@
    return
  ?hp < (maxhp / 2)
    equip_main_hand(vigor_1h)
  :?foe = aether
    equip_main_hand(vigor_1h)
  :?foe = fire
    equip_main_hand(aether_1h)
  :?foe = ice
    equip_main_hand(fire_1h)
  :?foe = poison
    equip_main_hand(ice_1h)
  :?foe = vigor
    equip_main_hand(poison_1h)
  :
    equip_main_hand(melee_dps)
  equip_offhand(mode)

func ranged(mode)
  _main_hand = ""
  ?(mode = "dps" | mode = "aoe" & ranged_2h ! "") |
  ^ ranged_1h = ""
    equip @ranged_2h@
    return
  ?(mode = "def" & ranged_1h ! "") | ranged_2h = ""
    equip_main_hand(ranged_1h)
    return equip_offhand(mode)
  return melee(mode)

// modes: "dps", "def", "aoe"
func fight(mode)
  ?foe.distance > 22
    return // continue to healwalk
  // xbow range
  ?foe.distance <= 22 & foe.distance > 7
    // medium range, but not marked as ranged
    ?foe = mine_walker |
    // made by hrminir
    ^foe = ice_wall |
    // poisons you when attacked by ranged
    ^foe = cult_guard |
    // don't shoot the brick wall
    ^foe = immune_to_ranged | 
    ^foe = ranged | foe = boss |
    // finish off remaining
    ^(foe.count < 3 & foe ! mosquito) |
    // spawns too fast
    ^foe = Toupee Snatcher |
    ^foe = spawner
      melee(mode)
    :? foe ! immune_to_physical
      ranged(mode)
    :
      melee(mode)
  // close range
  :?foe = immune_to_physical
    equip @elemental@
  :?should_slap()
    slap()
  :?should_smite()
    smite()
  :
    ?foe.count >= 5 | foe = spawner
      melee("aoe")
    :
      melee(mode)
  
// modes: "dps", "def", "aoe"
func fight_boss(mode)
  ?foe.distance > 22
    return // continue to healwalk
  ?should_hammer()
    hammer()
  :?should_bardiche()
    bardiche()
  :?foe.buffs.string = "buff_protection"
    fight(mode)
  :?foe.debuffs.string ! "∞:debuff_damage:1:" &
  ^ foe ! immune_to_debuff_damage
    equip_main_hand(poison_1h)
    equip_offhand(mode)
  :?foe.debuffs.string ! "φ:debuff_dot:1:" &
  ^foe ! immune_to_debuff_dot
    equip_main_hand(fire_1h)
    equip_offhand(mode)
  :?foe.debuffs.string ! chill &
  ^foe ! immune_to_debuff_chill
    equip_main_hand(ice_1h)
    equip_offhand(mode)
  :
    fight(mode)

// modes: "dps", "def", "aoe"
func default(mode)
  healwalk()
  ?foe ! boss
    fight(mode)
  :
    fight_boss(mode)

?loc = rocky
  healwalk()
  ?foe = dysangelos_elementalist
    fight("dps") // don't bother debuffing phase 2
  :?foe = dysangelos_perfected
    fight_boss("dps")
  :
    fight_boss("def")
  ?foe.state = 115 & foe.time = 60
    equipL mind stone // dodge the orb ray

?loc = deadwood
  ?foe.name=Poena
    // cheese her by staying out of range
    ?foe.buffs.string="poena_mirror"
      ranged("dps")
    :?item.GetCooldown("mind") <=0 &
    ^foe.distance < 23 & foe.state = 32 & foe.time > 37
      equipL mind stone
      equip_shield()
    :
      ranged("dps")
  :
    default("def")

?loc = caves
  default("def")

?loc = fungus_forest | loc = fungus_forest_boss
  default("def")

?loc = undead_crypt | loc = undead_crypt_boss
  default("def")

?loc = mine | loc = bronze_guardian
  healwalk()
  ?foe ! boss
    return default("def")
  ?foe.state = 32 & foe.time = 22
    equipR mind stone // dodge hammer
  :?foe.state = 33 // hammer is down
    default("dps")
  :?foe.distance <= 22
    equip @ranged_2h@

?loc = icy_ridge
  healwalk()
  ?foe ! boss
    return default("def")
  
  ?foe.armor > 0
    melee("dps")
  ?foe.distance <= 2
    fight_boss("dps")
  :?foe.distance <= 5
    // The shovel has an attack range of 1
    // so it forces the player to move very
    // very close. So close that it can avoid
    // the attacks (snowballs)
    equip shovel
  : // blown away
    melee("def")

func detect_poison(pos_x, pos_y)
  >`0,0,x=@x@, y=@y@
  for px = pos_x .. pos_x+10
    for py = pos_y-6 .. pos_y
      ?draw.GetSymbol(px, py) = "("
        cache.Add([pos_x, pos_y, px, py])
  var count = cache.Count()
  ?count > 0
    for line = 1 .. count
      var v = cache[line-1]
      >`0,@line@,x=@v[0]@, y=@v[1]@, px=@v[2]@, py=@v[3]@;

func is_poison_near(pos_x, pos_y)
  // long and mid range
  ?draw.GetSymbol(pos_x+2, pos_y-6) = "(" |
  // melee
  ^(draw.GetSymbol(pos_x+1, pos_y-6) = "(")
    return true
  return false

func fight_nagajara()
  // goal is to dodge poison balls
  x = screen.FromWorldX(pos.x)
  y = screen.FromWorldZ(pos.z)
  // detect_poison(x, y)
  ?is_poison_near(x, y)
    _naga++

  ?_naga = 0
    // start at the xbow range
    ranged("dps")
  :?_naga = 1
    // staff 1st poison
    equip quarterstaff
    activate R
    _naga++
  :?_naga = 2
    // and continue to shoot
    ranged("dps")
  :?_naga = 3
    // dash 2nd and 3rd poison
    // equipL @vigor_1h@
    // var dasher = dash()
    // ?dasher
    //   equipR @dasher@
    fight_boss("dps")
    _naga++
  :?_naga = 4
    fight_boss("dps")
  :?_naga >= 5
    ?item.GetCooldown("mind") <= 0
      // dodge back and repeat
      equipL mind stone
      equip_shield()
      _naga = 0
    :
      // mind stone on CD, try to evade
      fight_boss("def")

?loc = temple | loc = nagaraja
  ?foe ! boss
    default("def")
  :
    fight_nagajara()